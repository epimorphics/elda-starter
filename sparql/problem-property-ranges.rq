PREFIX api:               <http://purl.org/linked-data/api/vocab#>
PREFIX dc:                <http://purl.org/dc/elements/1.1/>
PREFIX dct:               <http://purl.org/dc/terms/>
PREFIX foaf:              <http://xmlns.com/foaf/0.1/>
PREFIX rdfs:              <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:               <http://www.w3.org/2002/07/owl#>
PREFIX qudt:              <http://qudt.org/schema/qudt#>
PREFIX qb:                <http://purl.org/linked-data/cube#>
PREFIX rdf:               <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:              <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:              <http://www.w3.org/2004/02/skos/core#>
PREFIX time:              <http://www.w3.org/2006/time#>
PREFIX void:              <http://rdfs.org/ns/void#>
PREFIX xsd:               <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?p ?r1 ?r2
WHERE
{
  # Pick a property and a random value. 
  { SELECT ?p (sample(?val) as ?v) 
    WHERE {
      ?s ?p ?val .
    } GROUP BY ?p
  } 

  # Get the property values' type (Resource or datatype).
  BIND (IF(!isLiteral(?v), rdfs:Resource, IF(datatype(?v)=rdf:langString || datatype(?v)=xsd:string,xsd:stringOrLangString, datatype(?v))) as ?r1)

  # Check whether there's another value for this property of a different type.
  # [Multiple literal datatypes in the range of properties are 'hard' to handle in LDA]

  [] ?p ?v2
  BIND (IF(!isLiteral(?v2), rdfs:Resource, IF(datatype(?v2)=rdf:langString || datatype(?v2)=xsd:string, xsd:stringOrLangString, datatype(?v2))) as ?r2)
  FILTER (STR(?r1) != STR(?r2))
} ORDER BY ?p ?r1 ?r2
